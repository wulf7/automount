#!/usr/bin/env perl

# Copyright (c) 2015 Vladimir Kondratiev <wulf@cicgroup.ru>
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that following conditions are met:
# 1. Redistributions of source code must retain the above copyright
#    notice, this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright
#    notice, this list of conditions and the following disclaimer in the
#    documentation and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE AUTHOR AND CONTRIBUTORS 'AS IS' AND ANY
# EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
# WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL THE AUTHOR OR CONTRIBUTORS BE LIABLE FOR ANY
# DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
# (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
# LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
# ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
# THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

use strict;
use warnings;
use POSIX qw(strftime locale_h);

sub usage () {
	printf "
Extracts vendor/product information of MTP-capable devices from libmtp sources
(http://libmtp.sourceforge.net/) and stores it in FreeBSD devd config format.

usage: perl $0 in-file out-file
  in-file:  path to src/music-players.h from libmtp
  out-file: path to devd configuration file
  dash symbol(-) can be used to redirect stdin/stdout

example: following can be used to update automount-mtp.conf from libmtp trunk
  curl 'http://sourceforge.net/p/libmtp/code/ci/master/tree/src/music-players.h?format=raw' | perl $0 - automount-mtp.conf
";
}

sub report ($$$) {
	my ($devicesp, $vid, $pidsp) = @_;
	my $ret = "\n";
	foreach (@$devicesp) {
		$ret .= "# $_\n";
	}

	$ret .= sprintf
'notify 100 {
	match "system"		"USB";
	match "subsystem"	"DEVICE";
	match "type"		"(ATTACH|DETACH)";
	match "vendor"		"%s";
	match "product"		"%s";
	action "/usr/local/sbin/automount $cdev $type";
};
', $vid, (@$pidsp > 1) ? "(".join('|', @$pidsp).")" : $pidsp->[0];

	return $ret;
}

my ($in, $out);
my ($device, $vid, $pid, $quirks);
my @devices;
my @pids;

my $in_file = $ARGV[0];
my $out_file = $ARGV[1];
my $prev_vid = undef;

setlocale(LC_TIME, "C");
$out = sprintf
'# devd configuration file for MTP-capable USB devices
#
# This file was automatically generated by running "%s %s %s"
# on "%s"
#
', $0, $in_file, $out_file, strftime("%a %b %e %H:%M:%S %Y", localtime());

if (!defined $in_file || !defined $out_file) {
	usage();
	exit();
}

open IN, "<$in_file" or die "cannot open $in_file: $!";
foreach (<IN>) {
	s/[^:]\/\/.*$//; # strip C++ style comments
	chomp;
	$in .= $_;
}
close IN;

$in =~ s/\/\*.*?\*\///g; # strip C style comments

while ($in =~ s/{ *"([^"]+)" *, *([x0-9a-f]+) *, *"([^"]+)" *, *([x0-9a-f]+) *,([^}]+)}//i) {
	$vid = $2;
	$pid = $4;
	$device = "$1 $3";
	$quirks = $5;
	if (defined $prev_vid && $vid ne $prev_vid) {
		$out .= report \@devices, $prev_vid, \@pids;
		@devices = ();
		@pids = ();
	}
	push @devices, $device;
	push @pids, $pid;
	$prev_vid = $vid;
}
if (!defined $vid) {
	die "No devices found! Is it src/music-players.h from libmtp?";
}
$out .= report \@devices, $vid, \@pids;

open OUT, "+>$out_file" or die "cannot open $out_file: $!";
printf OUT $out;
close OUT;
